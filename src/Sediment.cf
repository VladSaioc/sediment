comment "//" ;
comment "/*" "*/" ;

token Symbol '`' ((letter | ["$_"]) (letter | digit | ["$_\'"])*) '`';

-- Entry point
Spec_. Specification_ ::= [Def_] [Eval_];

-- Definitions
[]. [Def_] ::= ;
(:). [Def_] ::= Def_ [Def_] ;

-- -- Domain definitions
DomDef_. Def_ ::= "domain" Ident ":=" DomDefExp_ ";";
-- -- -- Union domains
DefUnion_. DomDefExp_ ::= Union_;
separator nonempty UnionBranch_"+";
UnionDom_. Union_ ::= "[" [UnionBranch_] "]";
TagDom_. UnionBranch_ ::= Ident "[" Dom_ "]";
Tag_. UnionBranch_ ::= Ident;
-- -- -- Non-union domains
coercions Dom_ 2;
DefNonUnion_. DomDefExp_ ::= Dom_;
FuncDom_. Dom_ ::= Dom_1 "->" Dom_;
ProdDom_. Dom_1 ::= Dom_2 "*" Dom_1;
VarDom_. Dom_2 ::= Ident;
IntDom_. Dom_2 ::= "Int";
BoolDom_. Dom_2 ::= "Bool";
SymDom_. Dom_2 ::= "Symbol";
StrDom_. Dom_2 ::= "String";

-- -- Syntax definitions
SyntaxDef_. Def_ ::= "syntax" Ident ":=" [FormRule_] ";";
separator nonempty FormRule_ "|";
Term_. FormRule_ ::= Ident "of" Dom_;
BareTerm_. FormRule_ ::= Ident;

-- -- Transition systems
TSystem_. Def_ ::= "system" TDom_ ":" Ident ":=" [Rule_] "end";
-- -- -- Transition system domains
BETDom_. TDom_ ::= Dom_ "|-" Dom_ "==>" Dom_;
NBETDom_. TDom_ ::= Dom_ "==>" Dom_;
-- -- -- Rules
terminator nonempty Rule_ ";";
BEAxiom_. Rule_ ::= "[" Ident "]:" Config_ "|-" Config_ "==>" Exp_;
NBEAxiom_. Rule_ ::= "[" Ident "]:" Config_ "==>" Exp_;
BERule_. Rule_ ::= "[" Ident "]:"  Config_ "|-" Config_ "==>" Exp_ "\\\\" [Premise_];
NBERule_. Rule_ ::= "[" Ident "]:" Config_ "==>" Exp_ "\\\\" [Premise_];
-- -- -- Premises
separator nonempty Premise_ ",";
IfPremise_. Premise_ ::= "if" Exp_1;
LetPremise_. Premise_ ::= "let" Ident ":=" Exp_1;
LetrecPremise_. Premise_ ::= "letrec" Dom_ ":" Ident ":=" Exp_1;
BETrInternalPremise_. Premise_ ::=  Exp_ "|-" Exp_ "==>" Config_;
BETrExternalPremise_. Premise_ ::=  Exp_ "|-" Exp_ "=" Ident "=>" Config_;
NBETrInternalPremise_. Premise_ ::=  Exp_ "==>" Config_;
NBETrExternalPremise_. Premise_ ::=  Exp_ "=" Ident "=>" Config_;
-- -- -- Configurations
separator nonempty Config_ ",";
TagConfig_. Config_ ::= Ident "[" Config_ "]";
PairConfig_. Config_ ::= "(" Config_ "," [Config_] ")";
BareTagConfig_. Config_ ::= Ident "[]";
VarConfig_. Config_ ::= Ident;
ConstConfig_. Config_ ::= Const_;

-- -- Data definitions
Data_. Def_ ::= "let" Ident ":=" Exp_1 ";";
DataRec_. Def_ ::= "letrec" Dom_ ":" Ident ":=" Exp_1 ";";

-- -- Expressions
coercions Exp_ 11;
separator nonempty Exp_1 ",";
Pair_. Exp_ ::= Exp_1 "," [Exp_1];
-- -- -- Lambda-calculus elements
Lambda_. Exp_1 ::= "\\" Dom_ ":" Ident "." Exp_1;

-- -- -- Extended functional expressions
Let_. Exp_1 ::= "let" Ident ":=" Exp_1 "in" Exp_1;
Letrec_. Exp_1 ::= "letrec" Dom_ ":" Ident ":=" Exp_1 "in" Exp_1;
If_. Exp_1 ::= "if" Exp_1 "then" Exp_1 "else" Exp_1;

-- -- -- Infix expressions
Concat_. Exp_2 ::= Exp_2 "++" Exp_3;
Plus_. Exp_3 ::= Exp_3 "+" Exp_4;
Minus_. Exp_3 ::= Exp_3 "-" Exp_4;
Prod_. Exp_4 ::= Exp_4 "*" Exp_5;
Div_. Exp_4 ::= Exp_4 "/" Exp_5;
Mod_. Exp_4 ::= Exp_4 "%" Exp_5;
Or_. Exp_5 ::= Exp_5 "|" Exp_6;
And_. Exp_6 ::= Exp_6 "&" Exp_7;
Equal_. Exp_7 ::= Exp_8 "==" Exp_8;
NotEqual_. Exp_7 ::= Exp_8 "!=" Exp_8;
LessThan_. Exp_7 ::= Exp_8 "<" Exp_8;
LessEqThan_. Exp_7 ::= Exp_8 "<=" Exp_8;
GreaterThan_. Exp_7 ::= Exp_8 ">" Exp_8;
GreaterEqThan_. Exp_7 ::= Exp_8 ">=" Exp_8;

-- -- -- Pairs and tuples
Head_. Exp_8 ::= "(<)" Exp_9;
Tail_. Exp_8 ::= "(>)" Exp_9;
Project_. Exp_8 ::= Exp_9 ">>" Ident;
Inject_. Exp_8 ::= Ident "[" Exp_ "]";
IsTag_. Exp_8 ::= Exp_9 "is" Ident;

-- -- -- Unary expressions
Inverse_. Exp_9 ::= "-" Exp_9;
Neg_. Exp_9 ::= "!" Exp_9;

-- -- -- Function application
App_. Exp_10 ::= Exp_10 "(" Exp_ ")";
Update_. Exp_10 ::= "[" Exp_ "->" Exp_ "]" Exp_11;

-- -- -- Variables and constants
Var_. Exp_11 ::= Ident;
ConstE_. Exp_11 ::= Const_;
BareTag_. Exp_11 ::= Ident "[]";
Bot_. Const_ ::= "_/" Dom_ "\\_";
Int_. Const_ ::= Integer;
Str_. Const_ ::= String;
Sym_. Const_ ::= Symbol;
BTrue_. Const_ ::= "true";
BFalse_. Const_ ::= "false";

-- Evaluations
terminator Eval_ ";";
BEEval_. Eval_ ::= "evaluate" Exp_ "|-" Exp_ "in" Ident;
NBEEval_. Eval_ ::= "evaluate" Exp_ "in" Ident;

entrypoints Specification_;